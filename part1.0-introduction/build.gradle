plugins {
    id 'java'
    id 'groovy'
    id 'io.github.platan.tests-execution-chart'
}

repositories {
    mavenCentral()
}

dependencies {
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    constraints {
        testImplementation('org.apache.groovy:groovy:4.0.18') {
            because 'version 4.0.6 pulled from spock-core causes "BUG! exception in phase [...] Unsupported class file major version 65"'
        }
    }
}

test {
    useJUnitPlatform()
    reports {
        junitXml {
            required = true
        }
    }
}

createTestsExecutionReport {
    formats {
        json {
            enabled = false
        }
        mermaid {
            enabled = false
        }
    }
    shiftTimestampsToStartOfDay = true
    marks {
        totalTimeOfAllTests {
            enabled = Boolean.parseBoolean(project.properties.getOrDefault('totalTimeOfAllTests', 'true') as String)
        }
    }
}
